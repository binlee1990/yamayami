label main3:
    jump main3_A

label main3_A:
    
    scene bg bg_s01
    with Fade(1, 0, 1)
    
    "みそぎとの一騒動があって以来、こんな田舎の学校では目立った事件も無く、日が過ぎていく。"
    
    "そして、梅雨前線は未だに日本列島に停滞中。"
    
    "本格的に晴れ、夏が来るのはいつだろうか。"
    
    play music "sound/bgm/bgm_001.ogg" fadein 2.0
    
    scene bg school classroom
    with Fade(1, 0, 1)
    
    "そんなこんなで、今日はあれから１週間余り経った土曜日。"
    
    "授業がある、といっても半ドンだ。"
    
    "先生" "「はぅ〜〜〜〜！！」"
    
    play sound "sound/se/se_033.ogg"
    
    "先生" "「じゅ、授業をはじめまーす！皆さん、席についてくださーい！！」"
    
    "……相変わらず騒々しいというか狙いすぎというか。いや本人は素なんだろうけど。"
    
    "この生徒よりちんまい先生は、……えっと、本名なんだっけ？"
    
    "皆から「ベッキー」と呼ばれていて、もはや本名など誰も気にしていないからなぁ……。"
    
    "……でも、３０近いのに「はう〜」は無いと思う。受ける奴には受けているが、正直聞いてる方が辛い……。"
    
    be "「では、教科書を開いてくださいー。今日は……菊池寛の『形』ですねー」"
    
    scene bg school classroom
    with Fade(1, 0, 1)
    
    be "「で、新兵衛はこの若武者に快く鎧を貸してあげたわけなのですが……さて、ここで問３ですね」"
    
    be "「『新兵衛がいつもの力を発揮できず、若武者が大活躍できたのは何故か』。３択です。では……雨宮くん、答えてみてください」"
    
    so "「んげ、俺か。えーっと……」"
    
    menu:
        "本人より鎧の意味のほうが重要になっていた":
            jump main3_A_1
            
        "実は敵にばれていて、新兵衛に敵が集中した":
            jump main3_A_2
            
        "新品の武具はなじまなかったから":
            jump main3_A_2

label main3_A_1:
    
    be "「正解です。現代でも、往々にしてある悲劇ですねー。皆さんも、肩書きや家名にとらわれて人を色眼鏡で見ちゃいけませんよー？」"
    
    be "「では、次の……」"
    
    so "「ふぅ。これで魅力＋１か」"
    
    ak "「『磨けば光る』くらいにはなったんじゃねーの？」"
    
    so "「……『カリスマ』までは遠いな」"
    
    be "「はい、そこー！無駄話はいけませんよー？」"
    
    so "「やべっ！」"
    
    jump main3_B

label main3_A_2:
    
    be "「残念。そんなんじゃ将来、肩書きに振り回される人生になりますよー？」"
    
    so "「……むぅ」"
    
    be "「でも皆さんはもう少し、教師という肩書きに敬意を持ってください。ちゃん付けで呼ぶなんてもってのほかですよ！」"
    
    "……どっちですか先生。というか、一部男子からは「たん」付けすらされているのですが。"
    
    jump main3_B

label main3_B:
    
    play music "sound/bgm/bgm_002.ogg" fadeout 2.0
    
    scene bg bg_s00
    with Fade(1, 0, 1)
    
    "俺は、この土曜日が好きだったりもする。"
    
    "昼になると、空の明るいうちに授業から解放される清々しさ。"
    
    "川に放流された人工孵化の鮭の稚魚のように、皆が拡散していくという感じだろうか。"
    
    "自由奔放、童心に帰ったような心地よさである。"
    
    "金曜の夜からだらだらと休みに入るより気分がいいのだ。"
    
    "梅雨だと言うのに、昨日からはずっと綺麗に晴れている。実にいい日だ。"
    
    scene bg school classroom
    with Dissolve(2)
    
    show akira aki_1_07
    with dissolve
    
    ak "「ふ、わぁ〜あ。なぁ、聡介、今日これからどうする？」"
    
    "明良だ。欠伸のついでに話しかけてきたような口調である。"
    
    "いつものことだ。気にしない。"
    
    so "「ああ。ごめんな。今日はお前の趣味本収集に付き合ってる暇はない」"
    
    show akira aki_1_05
    
    ak "「んな！？心外だなぁ。まさか、これだけ俺に付き合っておいて俺の趣味を全く理解していないとは！」"
    
    "本当に心外そうな顔で俺を睨めつける。"
    
    so "「本当じゃねぇかよ」"
    
    show akira aki_1_10
    
    ak "「いや、違うな！！アレはエロ本などではない！女性の裸体の曲線美！！その耽美を心行くまで堪能させてくれるという素晴らしき資料！」"
    
    ak "「神は女体という罪な造形美を俺たちの目の前に現出させながら、人はそれに触れてはいけないという！憎きかな倫理、道徳というものは！」"
    
    "社会一般的通念で言うと、こいつは変態だ。本人に言わせれば、一種芸術家なのだそうだが。"
    
    so "「わかったわかった。お前のその話は聞き飽きた」"
    
    show akira aki_1_04
    
    ak "「興味なさそうなこといってさ、お前だってこの前のあの写真しゅ、ううううああああ痛い痛い痛いイタイイタイ！！！！」"
    
    "まずいことを口走りそうになったので、取り急ぎの口封じでこめかみをぐりぐりしてやる。効果は抜群だ。"
    
    "俺は小声で、"
    
    so "「お前！！まだ教室に女子がいるだろ！！やめろ！！」"
    
    "明良も小声で、"
    
    ak "「今の今までエロ本の話してたやつが何で今更恥ずかしがるんだよ！！！」"
    
    "俺は反論する。"
    
    so "「当たり前だ！！お前がどう思われようと何も思わねぇが、俺が築いてきた清楚なイメージが崩れる！！」"
    
    "と、至極真っ当、普通の高校生が言い訳として使いそうな文句を拵えた。"
    
    show akira aki_1_01
    
    ak "「面白くないヤツだな。あ、ところでさ……」"
    
    "急に話題を変えやがった。まぁ、そのほうが俺としてはありがたいのだが。"
    
    ak "「何で俺につきあってくれねぇのよ？何の用事だ？」"
    
    "ああ、その話か。いや、特に無いが、今日はこんな暑苦しい輩と過ごしたい気分じゃない。"
    
    menu:
        "ちょっと、みそぎに話が……":
            $ loveMi += 1
            so "ちょっと、みそぎに話が……"
            
        "しとねと街に買い物に……":
            $ loveSi += 1
            so "しとねと街に買い物に……"
            
        "キリコ会長に呼ばれてて……":
            $ loveKi += 1
            so "キリコ会長に呼ばれてて……"
        
        "腹が痛いんだ……":
            $ loveAy += 1
            so "腹が痛いんだ……"
    
    "そういって俺は机の横に引っ掛けてあった鞄を取り、教科書類をぶち込む。"
    
    show akira aki_1_07
    
    ak "「そうかいそうかい！どうあっても俺とはつるみたくねぇってか！！友情に亀裂を感じるね、ふんっだ！！」"
    
    "こういうときだけ察しが良いらしい。"
    
    ak "「ああ、まぁいいぜ。どうせ毎週お前とつるんでもあれだしな。今日は他とどうにかやってみるわ」"
    
    so "「スマンな。何か悪いことしたみたいだ」"
    
    "まぁ、明良のことだ。持ち前の気さくさであるとか、部活でも友達がいるだろう。今日のところはそれで勘弁してもらうことにした。"
    
    show akira aki_1_01
    
    ak "「さて、それじゃ俺はそろそろ行くわ」"
    
    so "「ああ、それじゃまた月曜な」"
    
    hide akira
    with Dissolve(2)
    
    "明良と別れ、とりあえず教室を出る。"
    
    stop music fadeout 2.0
    
    scene image "#000000"
    with Dissolve(3)
    
    if loveAy >= 1:
        
        jump ayame1
    
    elif loveKi >= 1:
        
        jump kiriko1
    
    elif loveMi >= 1:
        
        jump misogi1
    
    elif loveSi >= 1:
        
        jump sitone1
    
    else:
        
        "（´・ω・） ｽ" "未実装（´・ω・） ｶﾜｲｿｽ..."
        
        return